pragma solidity ^0.8.34;
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "../utils/Constants.sol";
import "../interfaces/IUniswapRouter.sol";
import "../interfaces/IConvexMinter.sol";
import "../interfaces/IDSF.sol";
import "../interfaces/IConvexBooster.sol";
import "../interfaces/IConvexRewards.sol";
abstract contract CurveConvexStratBaseMk2 is Ownable {
    using SafeERC20 for IERC20Metadata;
    using SafeERC20 for IConvexMinter;
    enum WithdrawalType {
        Base,
        OneCoin
    }
    struct Config {
        IERC20Metadata[3] tokens;
        IERC20Metadata crv;
        IConvexMinter cvx;
        IUniswapRouter router;
        IConvexBooster booster;
        address[] cvxToUsdtPath;
        address[] crvToUsdtPath;
    }
    Config internal _config;
    IDSF public DSF;
    address public rewardManager;
    uint256 public constant UNISWAP_USD_MULTIPLIER = 1e12;
    uint256 public constant CURVE_PRICE_DENOMINATOR = 1e18;
    uint256 public constant DEPOSIT_DENOMINATOR = 10000;
    uint256 public constant DSF_DAI_TOKEN_ID = 0;
    uint256 public constant DSF_USDC_TOKEN_ID = 1;
    uint256 public constant DSF_USDT_TOKEN_ID = 2;
    uint256 public minDepositAmount = 9975;
    uint256 public swapSlippageBps = 9975;
    address public feeDistributor;
    bool public autoCompoundEnabled = true;
    uint256 public managementFees = 0;
    IERC20Metadata public poolLP;
    IConvexRewards public cvxRewards;
    uint256 public cvxPoolPID;
    uint256[4] public decimalsMultipliers;
    uint256 public minRewardToSell = 10 * 1e6;
    event SlippageUpdated(uint256 oldBps, uint256 newBps);
    event AutoCompoundToggled(bool enabled);
    event RouterUpdated(address indexed oldRouter, address indexed newRouter);
    event SoldRewards(uint256 cvxBalance, uint256 crvBalance, uint256 extraBalance);
    event RewardManagerUpdated(address indexed oldManager, address indexed newManager);
    event ManagerCompounded(uint256 usdtAmount, uint256 lpMinted);
    event StrategyDepositExecuted(
        uint256[3] amounts,
        uint256 poolLPsMinted,
        uint256 valueAddedUSDT
    );
    event StrategyWithdrawExecuted(
        address indexed withdrawer,
        uint256 removingCrvLps,
        uint256[3] actualOut
    );
    modifier onlyDSF() {
        require(_msgSender() == address(DSF), "must be called by DSF contract");
        _;
    }
    modifier onlyRewardManager() {
        require(_msgSender() == rewardManager, "only rewardManager");
        _;
    }
    constructor(Config memory config_, address poolLPAddr, address rewardsAddr, uint256 poolPID)
        Ownable(_msgSender())
    {
        _config = config_;
        for (uint256 i; i < 3; i++) {
            decimalsMultipliers[i] = calcTokenDecimalsMultiplier(_config.tokens[i]);
        }
        cvxPoolPID = poolPID;
        poolLP = IERC20Metadata(poolLPAddr);
        cvxRewards = IConvexRewards(rewardsAddr);
        feeDistributor = _msgSender();
    }
    function config() external view returns (Config memory) {
        return _config;
    }
    function deposit(uint256[3] memory amounts) external onlyDSF returns (uint256) {
        if (!checkDepositSuccessful(amounts)) {
            return 0;
        }
        uint256 poolLPs = depositPool(amounts);
        uint256 valueAdded = (poolLPs * getCurvePoolPrice()) / CURVE_PRICE_DENOMINATOR;
        emit StrategyDepositExecuted(
            amounts,
            poolLPs,
            valueAdded
        );
        return valueAdded;
    }
    function checkDepositSuccessful(uint256[3] memory amounts) internal view virtual returns (bool);
    function depositPool(uint256[3] memory amounts) internal virtual returns (uint256);
    function getCurvePoolPrice() internal view virtual returns (uint256);
    function transferAllTokensOut(address withdrawer, uint256[] memory prevBalances) internal {
        uint256 transferAmount;
        for (uint256 i = 0; i < 3; i++) {
            transferAmount =
                _config.tokens[i].balanceOf(address(this)) -
                prevBalances[i] -
                ((i == DSF_USDT_TOKEN_ID) ? managementFees : 0);
            if (transferAmount > 0) {
                _config.tokens[i].safeTransfer(withdrawer, transferAmount);
            }
        }
    }
    function transferDSFAllTokens() internal {
        uint256 transferAmount;
        for (uint256 i = 0; i < 3; i++) {
            uint256 managementFee = (i == DSF_USDT_TOKEN_ID) ? managementFees : 0;
            transferAmount = _config.tokens[i].balanceOf(address(this)) - managementFee;
            if (transferAmount > 0) {
                _config.tokens[i].safeTransfer(_msgSender(), transferAmount);
            }
        }
    }
    function calcWithdrawOneCoin(uint256 sharesAmount, uint128 tokenIndex)
        external
        view
        virtual
        returns (uint256 tokenAmount);
    function calcSharesAmount(uint256[3] memory tokenAmounts, bool isDeposit)
        external
        view
        virtual
        returns (uint256 sharesAmount);
    function withdraw(
        address withdrawer,
        uint256 userRatioOfCrvLps,
        uint256[3] memory tokenAmounts,
        WithdrawalType withdrawalType,
        uint128 tokenIndex
    ) external virtual onlyDSF returns (bool) {
        require(userRatioOfCrvLps > 0 && userRatioOfCrvLps <= 1e18, "Wrong lp Ratio");
        (bool success, uint256 removingCrvLps, uint256[] memory tokenAmountsDynamic) =
            calcCrvLps(withdrawalType, userRatioOfCrvLps, tokenAmounts, tokenIndex);
        if (!success) {
            return false;
        }
        uint256[] memory prevBalances = new uint256[](3);
        for (uint256 i = 0; i < 3; i++) {
            prevBalances[i] =
                _config.tokens[i].balanceOf(address(this)) -
                ((i == DSF_USDT_TOKEN_ID) ? managementFees : 0);
        }
        cvxRewards.withdrawAndUnwrap(removingCrvLps, false);
        removeCrvLps(removingCrvLps, tokenAmountsDynamic, withdrawalType, tokenAmounts, tokenIndex);
        uint256[3] memory actualOut;
        for (uint256 i = 0; i < 3; i++) {
            uint256 balNet =
                _config.tokens[i].balanceOf(address(this)) -
                ((i == DSF_USDT_TOKEN_ID) ? managementFees : 0);
            if (balNet > prevBalances[i]) {
                actualOut[i] = balNet - prevBalances[i];
            }
        }
        emit StrategyWithdrawExecuted(
            withdrawer,
            removingCrvLps,
            actualOut
        );
        transferAllTokensOut(withdrawer, prevBalances);
        return true;
    }
    function calcCrvLps(
        WithdrawalType withdrawalType,
        uint256 userRatioOfCrvLps,
        uint256[3] memory tokenAmounts,
        uint128 tokenIndex
    )
        internal
        virtual
        returns (bool success, uint256 removingCrvLps, uint256[] memory tokenAmountsDynamic);
    function removeCrvLps(
        uint256 removingCrvLps,
        uint256[] memory tokenAmountsDynamic,
        WithdrawalType withdrawalType,
        uint256[3] memory tokenAmounts,
        uint128 tokenIndex
    ) internal virtual;
    function calcTokenDecimalsMultiplier(IERC20Metadata token) internal view returns (uint256) {
        uint8 decimals = token.decimals();
        require(decimals <= 18, "DSF: wrong token decimals");
        if (decimals == 18) return 1;
        return 10 ** (18 - decimals);
    }
    function autoCompound() public onlyDSF {
        require(autoCompoundEnabled, "autocompound disabled");
        require(rewardManager != address(0), "rewardManager not set");
        try cvxRewards.getReward() {
        } catch {
            return;
        }
        _pushToken(_config.crv);
        _pushCvxToManager();
    }
    function managerCompound(uint256 amount) external onlyRewardManager {
        require(autoCompoundEnabled, "autocompound disabled");
        require(amount > 0, "zero amount");
        _config.tokens[DSF_USDT_TOKEN_ID].safeTransferFrom(rewardManager, address(this), amount);
        uint256[3] memory amounts;
        amounts[DSF_USDT_TOKEN_ID] = amount;
        uint256 lpMinted = depositPool(amounts);
        emit ManagerCompounded(amount, lpMinted);
    }
    function _pushToken(IERC20Metadata t) internal {
        uint256 bal = t.balanceOf(address(this));
        if (bal > 0) {
            t.safeTransfer(rewardManager, bal);
        }
    }
    function _pushCvxToManager() internal {
        uint256 bal = _config.cvx.balanceOf(address(this));
        if (bal > 0) {
            _config.cvx.safeTransfer(rewardManager, bal);
        }
    }
    function totalHoldings() public view virtual returns (uint256) {
        uint256 crvLpHoldings =
            (cvxRewards.balanceOf(address(this)) * getCurvePoolPrice()) / CURVE_PRICE_DENOMINATOR;
        uint256 crvEarned = cvxRewards.earned(address(this));
        uint256 cvxTotalCliffs = _config.cvx.totalCliffs();
        uint256 rpc = _config.cvx.reductionPerCliff();
        uint256 cliff = rpc == 0 ? 0 : _config.cvx.totalSupply() / rpc;
        uint256 cvxRemainCliffs = cliff < cvxTotalCliffs ? (cvxTotalCliffs - cliff) : 0;
        uint256 amountIn =
            (cvxTotalCliffs == 0)
                ? _config.cvx.balanceOf(address(this))
                : (crvEarned * cvxRemainCliffs) / cvxTotalCliffs + _config.cvx.balanceOf(address(this));
        uint256 cvxEarningsUSDT = priceTokenByExchange(amountIn, _config.cvxToUsdtPath);
        amountIn = crvEarned + _config.crv.balanceOf(address(this));
        uint256 crvEarningsUSDT = priceTokenByExchange(amountIn, _config.crvToUsdtPath);
        uint256 rewardsGrossUSDT = cvxEarningsUSDT + crvEarningsUSDT;
        if (rewardsGrossUSDT > 0) {
            uint256 feeUSDT = DSF.calcManagementFee(rewardsGrossUSDT);
            if (feeUSDT < rewardsGrossUSDT) {
                rewardsGrossUSDT -= feeUSDT;
            } else {
                rewardsGrossUSDT = 0;
            }
        }
        uint256 tokensHoldings = 0;
        for (uint256 i = 0; i < 3; i++) {
            tokensHoldings += _config.tokens[i].balanceOf(address(this)) * decimalsMultipliers[i];
        }
        return
            tokensHoldings +
            crvLpHoldings +
            rewardsGrossUSDT * decimalsMultipliers[DSF_USDT_TOKEN_ID];
    }
    function priceTokenByExchange(uint256 amountIn, address[] memory path)
        internal
        view
        returns (uint256)
    {
        if (amountIn == 0) return 0;
        if (path.length < 2) return 0;
        try _config.router.getAmountsOut(amountIn, path) returns (uint256[] memory amounts) {
            if (amounts.length == 0) return 0;
            if (amounts.length < path.length) return 0;
            return amounts[amounts.length - 1];
        } catch {
            return 0;
        }
    }
    function getCVXCRVHoldingsGross()
        external
        view
        returns (uint256 amountIn_cvx, uint256 amountIn_crv, uint256 cvxEarningsUSDT, uint256 crvEarningsUSDT)
    {
        uint256 crvEarned = cvxRewards.earned(address(this));
        uint256 cvxTotalCliffs = _config.cvx.totalCliffs();
        uint256 rpc = _config.cvx.reductionPerCliff();
        uint256 cliff = rpc == 0 ? 0 : _config.cvx.totalSupply() / rpc;
        uint256 cvxRemainCliffs = cliff < cvxTotalCliffs ? (cvxTotalCliffs - cliff) : 0;
        amountIn_cvx =
            (cvxTotalCliffs == 0)
                ? _config.cvx.balanceOf(address(this))
                : (crvEarned * cvxRemainCliffs) / cvxTotalCliffs + _config.cvx.balanceOf(address(this));
        amountIn_crv = crvEarned + _config.crv.balanceOf(address(this));
        cvxEarningsUSDT = (amountIn_cvx == 0) ? 0 : priceTokenByExchange(amountIn_cvx, _config.cvxToUsdtPath);
        crvEarningsUSDT = (amountIn_crv == 0) ? 0 : priceTokenByExchange(amountIn_crv, _config.crvToUsdtPath);
    }
    function getCVXCRVHoldings()
        external
        view
        returns (uint256 amountIn_cvx, uint256 amountIn_crv, uint256 cvxEarningsUSDT, uint256 crvEarningsUSDT)
    {
        uint256 crvEarned = cvxRewards.earned(address(this));
        uint256 cvxTotalCliffs = _config.cvx.totalCliffs();
        uint256 rpc = _config.cvx.reductionPerCliff();
        uint256 cliff = rpc == 0 ? 0 : _config.cvx.totalSupply() / rpc;
        uint256 cvxRemainCliffs = cliff < cvxTotalCliffs ? (cvxTotalCliffs - cliff) : 0;
        uint256 amountIn_cvx_gross =
            (cvxTotalCliffs == 0)
                ? _config.cvx.balanceOf(address(this))
                : (crvEarned * cvxRemainCliffs) / cvxTotalCliffs + _config.cvx.balanceOf(address(this));
        uint256 amountIn_crv_gross = crvEarned + _config.crv.balanceOf(address(this));
        uint256 cvxGrossUSDT =
            (amountIn_cvx_gross == 0) ? 0 : priceTokenByExchange(amountIn_cvx_gross, _config.cvxToUsdtPath);
        uint256 crvGrossUSDT =
            (amountIn_crv_gross == 0) ? 0 : priceTokenByExchange(amountIn_crv_gross, _config.crvToUsdtPath);
        uint256 totalGrossUSDT = cvxGrossUSDT + crvGrossUSDT;
        amountIn_cvx = amountIn_cvx_gross;
        amountIn_crv = amountIn_crv_gross;
        cvxEarningsUSDT = cvxGrossUSDT;
        crvEarningsUSDT = crvGrossUSDT;
        if (totalGrossUSDT == 0) return (amountIn_cvx, amountIn_crv, cvxEarningsUSDT, crvEarningsUSDT);
        uint256 totalFeeUSDT = DSF.calcManagementFee(totalGrossUSDT);
        if (totalFeeUSDT >= totalGrossUSDT) return (0, 0, 0, 0);
        uint256 feeCvxUSDT = (totalFeeUSDT * cvxGrossUSDT) / totalGrossUSDT;
        uint256 feeCrvUSDT = totalFeeUSDT - feeCvxUSDT;
        cvxEarningsUSDT = cvxGrossUSDT - feeCvxUSDT;
        crvEarningsUSDT = crvGrossUSDT - feeCrvUSDT;
        if (feeCvxUSDT > 0 && cvxGrossUSDT > 0 && amountIn_cvx_gross > 0) {
            uint256 feeCvxTokens = (feeCvxUSDT * amountIn_cvx_gross) / cvxGrossUSDT;
            amountIn_cvx = (feeCvxTokens >= amountIn_cvx_gross) ? 0 : (amountIn_cvx_gross - feeCvxTokens);
        }
        if (feeCrvUSDT > 0 && crvGrossUSDT > 0 && amountIn_crv_gross > 0) {
            uint256 feeCrvTokens = (feeCrvUSDT * amountIn_crv_gross) / crvGrossUSDT;
            amountIn_crv = (feeCrvTokens >= amountIn_crv_gross) ? 0 : (amountIn_crv_gross - feeCrvTokens);
        }
        return (amountIn_cvx, amountIn_crv, cvxEarningsUSDT, crvEarningsUSDT);
    }
    function claimManagementFees() public returns (uint256) {
        uint256 usdtBalance = _config.tokens[DSF_USDT_TOKEN_ID].balanceOf(address(this));
        uint256 transferBalance = managementFees > usdtBalance ? usdtBalance : managementFees;
        if (transferBalance > 0) {
            _config.tokens[DSF_USDT_TOKEN_ID].safeTransfer(feeDistributor, transferBalance);
        }
        managementFees = 0;
        return transferBalance;
    }
    function updateMinDepositAmount(uint256 _minDepositAmount) public onlyOwner {
        require(_minDepositAmount > 0 && _minDepositAmount <= 10000, "Wrong amount!");
        minDepositAmount = _minDepositAmount;
    }
    function renounceOwnership() public view override onlyOwner {
        revert("The strategy must have an owner");
    }
    function setDSF(address DSFAddr) external onlyOwner {
        DSF = IDSF(DSFAddr);
    }
    function setRouter(address newRouter) external onlyOwner {
        require(newRouter != address(0), "router is zero");
        address old = address(_config.router);
        _config.router = IUniswapRouter(newRouter);
        emit RouterUpdated(old, newRouter);
    }
    function setRewardManager(address newManager) external onlyOwner {
        require(newManager != address(0), "zero addr");
        emit RewardManagerUpdated(rewardManager, newManager);
        rewardManager = newManager;
    }
    function updateSwapSlippageBps(uint256 _newBps) external onlyOwner {
        require(_newBps > 0 && _newBps <= 10_000, "Wrong bps");
        uint256 old = swapSlippageBps;
        swapSlippageBps = _newBps;
        emit SlippageUpdated(old, _newBps);
    }
    function toggleAutoCompound(bool _enable) external onlyOwner {
        autoCompoundEnabled = _enable;
        emit AutoCompoundToggled(_enable);
    }
    function updateMinRewardToSell(uint256 _minRewardToSell) external onlyOwner {
        minRewardToSell = _minRewardToSell;
    }
    function withdrawStuckToken(IERC20Metadata _token) external onlyOwner {
        uint256 tokenBalance = _token.balanceOf(address(this));
        if (tokenBalance > 0) {
            _token.safeTransfer(_msgSender(), tokenBalance);
        }
    }
    function changeFeeDistributor(address _feeDistributor) external onlyOwner {
        feeDistributor = _feeDistributor;
    }
    function toArr2(uint256[] memory arrInf) internal pure returns (uint256[2] memory arr) {
        arr[0] = arrInf[0];
        arr[1] = arrInf[1];
    }
    function fromArr2(uint256[2] memory arr) internal pure returns (uint256[] memory arrInf) {
        arrInf = new uint256[](2);
        arrInf[0] = arr[0];
        arrInf[1] = arr[1];
    }
    function toArr3(uint256[] memory arrInf) internal pure returns (uint256[3] memory arr) {
        arr[0] = arrInf[0];
        arr[1] = arrInf[1];
        arr[2] = arrInf[2];
    }
    function fromArr3(uint256[3] memory arr) internal pure returns (uint256[] memory arrInf) {
        arrInf = new uint256[](3);
        arrInf[0] = arr[0];
        arrInf[1] = arr[1];
        arrInf[2] = arr[2];
    }
    function toArr4(uint256[] memory arrInf) internal pure returns (uint256[4] memory arr) {
        arr[0] = arrInf[0];
        arr[1] = arrInf[1];
        arr[2] = arrInf[2];
        arr[3] = arrInf[3];
    }
    function fromArr4(uint256[4] memory arr) internal pure returns (uint256[] memory arrInf) {
        arrInf = new uint256[](4);
        arrInf[0] = arr[0];
        arrInf[1] = arr[1];
        arrInf[2] = arr[2];
        arrInf[3] = arr[3];
    }
}